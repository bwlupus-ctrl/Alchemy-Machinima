/**
 * @file class1/deferred/deferredUtil.glsl
 *
 * $LicenseInfo:firstyear=2007&license=viewerlgpl$
 * Second Life Viewer Source Code
 * Copyright (C) 2007, Linden Research, Inc.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation;
 * version 2.1 of the License only.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * Linden Research, Inc., 945 Battery Street, San Francisco, CA  94111  USA
 * $/LicenseInfo$
 */


/*  Parts of this file are taken from Sascha Willem's Vulkan GLTF refernce implementation
MIT License

Copyright (c) 2018 Sascha Willems

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

uniform sampler2D normalMap;
uniform sampler2D depthMap;
uniform sampler2D projectionMap; // rgba
uniform sampler2D brdfLut;

// projected lighted params
uniform mat4 proj_mat; //screen space to light space projector
uniform vec3 proj_n; // projector normal
uniform vec3 proj_p; //plane projection is emitting from (in screen space)
uniform float proj_focus; // distance from plane to begin blurring
uniform float proj_lod  ; // (number of mips in proj map)
uniform float proj_range; // range between near clip and far clip plane of projection
uniform float proj_ambiance;

uniform int classic_mode;

// [BDMerge Batch 2] Feature 2 - gobo/cookie mip + anisotropic filtering.
// When non-zero, the cookie LOD samplers clamp their focus-based LOD up to the
// screen-space (derivative) minification LOD, so a projected gobo viewed at a
// grazing angle or from far away samples a coarser mip instead of aliasing.
// Only the surface projector programs upload this (setupSpotLight); it stays 0
// for every other consumer, keeping their sampling byte-identical.
uniform int gobo_aniso;
uniform float gobo_time;
uniform int gobo_pattern;
// x = animation mode, y = speed, z = zoom, w = surface-only dispersion.
uniform vec4 gobo_anim_params;
// [Gobo v2] Per-projector cookie color multiply. Default (1,1,1) = no change.
uniform vec3 gobo_tint;
// [Gobo v2] Per-pattern variation. Each component 0 = the pattern's built-in
// default (so an override with zero params reproduces the v1 look exactly):
// x = detail/count, y = thickness/tilt, z = softness, w = invert (>=0.5).
uniform vec4 gobo_pattern_params;

// light params
uniform vec3 color; // light_color
uniform float size; // light_size

// [BDMerge Batch 2] Screen-space minification LOD from the cookie UV derivatives.
// Returns the base (focus) LOD raised to the minification LOD so grazing/distant
// gobos never sample a finer mip than the pixel footprint warrants (anti-alias).
// The C++ side sets a trilinear-mip + anisotropic filter state on the cookie so
// the extra mip levels this selects are actually present and crisply filtered.
float goboLod(vec2 tc, float base_lod)
{
#ifndef FXAA_GLSL_120
    if (gobo_aniso != 0)
    {
        vec2  ts  = vec2(textureSize(projectionMap, 0));
        vec2  dx  = dFdx(tc) * ts;
        vec2  dy  = dFdy(tc) * ts;
        float d2  = max(dot(dx, dx), dot(dy, dy));
        float slod = 0.5 * log2(max(d2, 1.0e-8));
        return max(base_lod, slod);
    }
#endif
    return base_lod;
}

float goboHash(vec2 p)
{
    return fract(sin(dot(p, vec2(127.1, 311.7))) * 43758.5453);
}

float goboValueNoise(vec2 p)
{
    vec2 i = floor(p);
    vec2 f = fract(p);
    f = f * f * (3.0 - 2.0 * f);
    return mix(mix(goboHash(i), goboHash(i + vec2(1.0, 0.0)), f.x),
               mix(goboHash(i + vec2(0.0, 1.0)),
                   goboHash(i + vec2(1.0, 1.0)), f.x), f.y);
}

// Analytic library. `pp` = gobo_pattern_params (x detail/count, y thickness/tilt,
// z softness, w invert). EVERY component 0 reproduces the pattern's v1 constants
// exactly, so a default override renders byte-identically to the pre-v2 build.
// Pattern ids 100+ stay reserved for a future PNG library (loader modeled on
// loadProjVolDustMap(), shipped via viewer_manifest.py) with no caller change.
vec4 proceduralGobo(int id, vec2 uv, vec4 pp)
{
    float pattern = 1.0;
    float detail = exp2(pp.x);   // 1.0 at default; scales count/frequency
    if (id == 0)
    {
        // Venetian blinds: soft horizontal slats.
        float band = abs(fract(uv.y * 12.0 * detail) - 0.5);
        float t = pp.y * 0.12;
        float soft = pp.z * 0.15;
        pattern = 1.0 - smoothstep(0.30 - t - soft, 0.40 - t + soft, band);
    }
    else if (id == 1)
    {
        // Multi-pane window with a frame and central mullions.
        vec2 p = abs(fract(uv * 2.0 * detail) - 0.5);
        float th = 0.43 - pp.y * 0.15;
        float bars = max(step(th, p.x), step(th, p.y));
        pattern = 1.0 - bars;
    }
    else if (id == 2)
    {
        // Crosshatched grate.
        vec2 bars = abs(fract(uv * 10.0 * detail) - 0.5);
        float th = 0.34 + pp.y * 0.10;
        pattern = 1.0 - max(step(th, bars.x), step(th, bars.y));
    }
    else if (id == 3)
    {
        // Ceiling-fan blades around the hub (blade count via detail).
        vec2 p = uv - vec2(0.5);
        float radius = length(p);
        float blade = abs(fract((atan(p.y, p.x) / 6.2831853) * 5.0 * detail) - 0.5);
        pattern = (1.0 - smoothstep(0.13, 0.23, blade)) *
                  smoothstep(0.06, 0.12, radius) * (1.0 - smoothstep(0.46, 0.50, radius));
    }
    else if (id == 4)
    {
        // Softbox square-cell grid.
        vec2 cell = abs(fract(uv * 8.0 * detail) - 0.5);
        float seam = max(cell.x, cell.y);
        float soft = pp.z * 0.10;
        pattern = 1.0 - smoothstep(0.38 - soft, 0.47 + soft, seam);
    }
    else if (id == 5)
    {
        // Organic foliage/dapple breakup from two value-noise octaves.
        float sc = 8.0 * detail;
        float n = goboValueNoise(uv * sc) * 0.65 +
                  goboValueNoise(uv * sc * 2.125 + vec2(3.7, 9.2)) * 0.35;
        float lo = 0.42 - pp.y * 0.2;
        pattern = smoothstep(lo, lo + 0.20, n);
    }
    else if (id == 6)
    {
        // Cheap caustic-like interference field; UV motion supplies the scroll.
        vec2 p = (uv - vec2(0.5)) * 9.0 * detail;
        float a = sin(p.x + sin(p.y * 1.7));
        float b = sin(p.y * 1.3 - sin(p.x * 1.9));
        pattern = smoothstep(0.25, 0.82, abs(a + b) * 0.5);
    }
    else if (id == 7)
    {
        // Prison / warehouse heavy vertical bars.
        float bar = abs(fract(uv.x * 6.0 * detail) - 0.5);
        float soft = pp.z * 0.10;
        pattern = 1.0 - smoothstep(0.24 - soft, 0.32 + soft, bar);
    }
    else if (id == 8)
    {
        // Cathedral arched window: rounded-top aperture with mullions.
        float body = step(0.20, uv.x) * step(uv.x, 0.80) *
                     step(0.10, uv.y) * step(uv.y, 0.65);
        float arch = step(length((uv - vec2(0.5, 0.65)) / vec2(0.30, 0.35)), 1.0) *
                     step(0.65, uv.y);
        float shape = clamp(body + arch, 0.0, 1.0);
        vec2 m = abs(fract(uv * vec2(3.0, 4.0) * detail) - 0.5);
        float bars = max(step(0.42, m.x), step(0.42, m.y));
        pattern = shape * (1.0 - bars);
    }
    else if (id == 9)
    {
        // Iris / vignette framing aperture (oval via thickness, soft edge).
        float rad = 0.45 * exp2(-pp.x * 0.5);
        float soft = 0.04 + pp.z * 0.40;
        vec2 d = (uv - vec2(0.5)) * vec2(1.0 + pp.y, 1.0);
        pattern = 1.0 - smoothstep(rad - soft, rad, length(d));
    }
    else if (id == 10)
    {
        // Dot-matrix / disco grid.
        float count = 8.0 * detail;
        vec2 cell = fract(uv * count) - 0.5;
        float rad = 0.30 + pp.y * 0.15;
        float soft = 0.05 + pp.z * 0.20;
        pattern = 1.0 - smoothstep(rad, rad + soft, length(cell));
    }
    else if (id == 11)
    {
        // Concentric rings blending to radial starburst (blend via thickness).
        float count = 8.0 * detail;
        float rings = abs(fract(length(uv - vec2(0.5)) * count) - 0.5);
        float ringp = 1.0 - smoothstep(0.22, 0.32, rings);
        float ang = atan(uv.y - 0.5, uv.x - 0.5);
        float sp = abs(fract(ang / 6.2831853 * max(count, 1.0)) - 0.5);
        float spokep = 1.0 - smoothstep(0.22, 0.32, sp);
        pattern = mix(ringp, spokep, clamp(pp.y, 0.0, 1.0));
    }
    else if (id == 12)
    {
        // Soft cloud / god-ray breakup (softer threshold than foliage).
        float sc = 4.0 * detail;
        float n = goboValueNoise(uv * sc) * 0.6 +
                  goboValueNoise(uv * sc * 2.25 + vec2(5.0, 2.0)) * 0.4;
        float lo = 0.35 - pp.y * 0.2;
        pattern = smoothstep(lo, lo + 0.40, n);
    }
    pattern = clamp(pattern, 0.0, 1.0);
    if (pp.w >= 0.5)
    {
        pattern = 1.0 - pattern; // negative cookie
    }
    return vec4(vec3(pattern), 1.0);
}

// UV-space animation modes: 1 rotate, 2 wind-sway, 3 pan, 4 pulse (osc zoom).
// Modes 0/5/6 leave the UV unchanged (5/6 are intensity-only, see below).
vec2 animatedGoboUV(vec2 uv)
{
    int mode = int(gobo_anim_params.x + 0.5);
    float speed = gobo_anim_params.y;
    float zoom = max(gobo_anim_params.z, 0.001);
    if (mode == 4) // pulse/breathe oscillates the zoom about its base
    {
        zoom = max(zoom * (1.0 + 0.25 * sin(speed * gobo_time)), 0.001);
    }
    // Preserve the legacy texture coordinates bit-for-bit when nothing moves
    // the UV (mode 0/5/6 with default zoom).
    if (mode != 1 && mode != 2 && mode != 3 && zoom == 1.0)
    {
        return uv;
    }
    uv = (uv - vec2(0.5)) / zoom + vec2(0.5);
    if (mode == 1) // continuous rotation
    {
        float angle = speed * gobo_time;
        float c = cos(angle);
        float s = sin(angle);
        mat2 rotation = mat2(c, s, -s, c);
        uv = rotation * (uv - vec2(0.5)) + vec2(0.5);
    }
    else if (mode == 2) // wind sway domain warp
    {
        vec2 phase = uv.yx * vec2(9.0, 7.0) +
                     gobo_time * speed * vec2(1.0, 1.31);
        uv += 0.018 * sin(phase);
    }
    else if (mode == 3) // pan / scroll along a fixed diagonal
    {
        uv += normalize(vec2(1.0, 0.35)) * speed * gobo_time * 0.1;
    }
    return uv;
}

// Intensity-space animation modes: 5 flicker (candle/fire), 6 strobe. Every
// other mode returns 1.0, so this is a no-op unless flicker/strobe is chosen.
float goboAnimIntensity()
{
    int mode = int(gobo_anim_params.x + 0.5);
    float speed = max(abs(gobo_anim_params.y), 0.001);
    if (mode == 5)
    {
        float n = goboValueNoise(vec2(gobo_time * speed * 3.0, 0.0));
        return mix(0.6, 1.0, n);
    }
    if (mode == 6)
    {
        return step(fract(gobo_time * speed), 0.5);
    }
    return 1.0;
}

// Shared by surface lighting, froxel injection and the volumetric march. Tint
// (default white) and intensity (default 1.0) keep the off-path byte-identical.
vec4 sampleGobo(vec2 uv, float lod)
{
    vec2 animated_uv = animatedGoboUV(uv);
    vec4 col;
    if (gobo_pattern >= 0)
    {
        col = proceduralGobo(gobo_pattern, animated_uv, gobo_pattern_params);
    }
    else
    {
#ifndef FXAA_GLSL_120
        col = textureLod(projectionMap, animated_uv, goboLod(animated_uv, lod));
#else
        col = texture(projectionMap, animated_uv);
#endif
    }
    col.rgb *= gobo_tint;
    col.rgb *= goboAnimIntensity();
    return col;
}

uniform mat4 inv_proj;
uniform vec2 screen_res;

const float M_PI = 3.14159265;
const float ONE_OVER_PI = 0.3183098861;

const float MIN_PBR_ROUGHNESS = 0.045;

vec3 srgb_to_linear(vec3 cs);
vec3 linear_to_srgb(vec3 cs);
vec3 atmosFragLightingLinear(vec3 light, vec3 additive, vec3 atten);

vec4 decodeNormal(vec4 norm);

vec3 clampHDRRange(vec3 color)
{
    // Why do this?
    // There are situations where the color range will go to something insane - potentially producing infs and NaNs even.
    // This is a safety measure to prevent that.
    // -Geenz 2025-03-05
    color = mix(color, vec3(1), isinf(color));
    color = mix(color, vec3(0.0), isnan(color));
    return color;
}

float calcLegacyDistanceAttenuation(float distance, float falloff)
{
    float dist_atten = 1.0 - clamp((distance + falloff)/(1.0 + falloff), 0.0, 1.0);
    dist_atten *= dist_atten;

    // Tweak falloff slightly to match pre-EEP attenuation
    // NOTE: this magic number also shows up in a great many other places, search for dist_atten *= to audit
    dist_atten *= 2.0;
    return dist_atten;
}

// In:
//   lv  unnormalized surface to light vector
//   n   normal of the surface
//   pos unnormalized camera to surface vector
// Out:
//   l   normalized surace to light vector
//   nl  diffuse angle
//   nh  specular angle
void calcHalfVectors(vec3 lv, vec3 n, vec3 v,
    out vec3 h, out vec3 l, out float nh, out float nl, out float nv, out float vh, out float lightDist)
{
    l  = normalize(lv);
    h  = normalize(l + v);

    // lower bound to avoid divide by zero
    float eps = 0.000001;
    nh = clamp(dot(n, h), eps, 1.0);
    nl = clamp(dot(n, l), eps, 1.0);
    nv = clamp(dot(n, v), eps, 1.0);
    vh = clamp(dot(v, h), eps, 1.0);

    lightDist = length(lv);
}

// In:
//   light_center
//   pos
// Out:
//   dist
//   l_dist
//   lv
//   proj_tc  Projector Textue Coordinates
bool clipProjectedLightVars(vec3 light_center, vec3 pos, out float dist, out float l_dist, out vec3 lv, out vec4 proj_tc )
{
    lv = light_center - pos.xyz;
    dist = length(lv);
    bool clipped = (dist >= size);
    if ( !clipped )
    {
        dist /= size;

        l_dist = -dot(lv, proj_n);
        vec4 projected_point = (proj_mat * vec4(pos.xyz, 1.0));
        clipped = (projected_point.z < 0.0);
        projected_point.xyz /= projected_point.w;
        proj_tc = projected_point;
    }

    return clipped;
}

vec2 getScreenCoordinate(vec2 screenpos)
{
    vec2 sc = screenpos.xy * 2.0;
    return sc - vec2(1.0, 1.0);
}

vec4 getNorm(vec2 screenpos)
{
    vec4 norm = decodeNormal(texture(normalMap, screenpos.xy));
    return norm;
}

vec4 getNormRaw(vec2 screenpos)
{
    vec4 norm = texture(normalMap, screenpos.xy);
    return norm;
}

// get linear depth value given a depth buffer sample d and znear and zfar values
float linearDepth(float d, float znear, float zfar)
{
    d = d * 2.0 - 1.0;
    return znear * 2.0 * zfar / (zfar + znear - d * (zfar - znear));
}

float linearDepth01(float d, float znear, float zfar)
{
    return linearDepth(d, znear, zfar) / zfar;
}

float getDepth(vec2 pos_screen)
{
    float depth = texture(depthMap, pos_screen).r;
    return depth;
}

vec4 getTexture2DLodAmbient(vec2 tc, float lod)
{
    vec4 ret = sampleGobo(tc, lod);
    ret.rgb = srgb_to_linear(ret.rgb);

    vec2 dist = tc-vec2(0.5);
    float d = dot(dist,dist);
    ret *= min(clamp((0.25-d)/0.25, 0.0, 1.0), 1.0);

    return ret;
}

vec4 getTexture2DLodDiffuse(vec2 tc, float lod)
{
    vec4 ret = sampleGobo(tc, lod);
    ret.rgb = srgb_to_linear(ret.rgb);

    vec2 dist = vec2(0.5) - abs(tc-vec2(0.5));
    float det = min(lod/(proj_lod*0.5), 1.0);
    float d = min(dist.x, dist.y);
    float edge = 0.25*det;
    ret *= clamp(d/edge, 0.0, 1.0);

    return ret;
}

// lit     This is set by the caller: if (nl > 0.0) { lit = attenuation * nl * noise; }
// Uses:
//   color   Projected spotlight color
vec3 getProjectedLightAmbiance(float amb_da, float attenuation, float lit, float nl, float noise, vec2 projected_uv)
{
    vec4 amb_plcol = getTexture2DLodAmbient(projected_uv, proj_lod);
    vec3 amb_rgb   = amb_plcol.rgb * amb_plcol.a;

    amb_da += proj_ambiance;
    amb_da += (nl*nl*0.5+0.5) * proj_ambiance;
    amb_da *= attenuation * noise;
    amb_da = min(amb_da, 1.0-lit);

    return (amb_da * color.rgb * amb_rgb);
}

// Returns projected light in Linear
// Uses global spotlight color:
//  color
// NOTE: projected.a will be pre-multiplied with projected.rgb
vec3 getProjectedLightDiffuseColor(float light_distance, vec2 projected_uv)
{
    float diff = clamp((light_distance - proj_focus)/proj_range, 0.0, 1.0);
    float lod = diff * proj_lod;
    vec4 plcol = getTexture2DLodDiffuse(projected_uv.xy, lod);
    // Surface-only chromatic split. Volumetric consumers call the single-tap
    // getTexture2DLodDiffuse funnel directly and never enter this branch.
    float dispersion = max(gobo_anim_params.w, 0.0);
    if (dispersion > 0.0)
    {
        vec2 radial = projected_uv - vec2(0.5);
        vec4 red_sample = getTexture2DLodDiffuse(projected_uv + radial * dispersion, lod);
        vec4 blue_sample = getTexture2DLodDiffuse(projected_uv - radial * dispersion, lod);
        plcol.rgb = vec3(red_sample.r, plcol.g, blue_sample.b);
    }

    return color.rgb * plcol.rgb * plcol.a;
}

vec4 texture2DLodSpecular(vec2 tc, float lod)
{
    vec4 ret = sampleGobo(tc, lod);
    ret.rgb = srgb_to_linear(ret.rgb);

    vec2 dist = vec2(0.5) - abs(tc-vec2(0.5));
    float det = min(lod/(proj_lod*0.5), 1.0);
    float d = min(dist.x, dist.y);
    d *= min(1, d * (proj_lod - lod)); // BUG? extra factor compared to diffuse causes N repeats
    float edge = 0.25*det;
    ret *= clamp(d/edge, 0.0, 1.0);

    return ret;
}

// See: clipProjectedLightVars()
vec3 getProjectedLightSpecularColor(vec3 pos, vec3 n )
{
    vec3 slit = vec3(0);
    vec3 ref = reflect(normalize(pos), n);

    //project from point pos in direction ref to plane proj_p, proj_n
    vec3 pdelta = proj_p-pos;
    float l_dist = length(pdelta);
    float ds = dot(ref, proj_n);
    if (ds < 0.0)
    {
        vec3 pfinal = pos + ref * dot(pdelta, proj_n)/ds;
        vec4 stc = (proj_mat * vec4(pfinal.xyz, 1.0));
        if (stc.z > 0.0)
        {
            stc /= stc.w;
            slit = getProjectedLightDiffuseColor( l_dist, stc.xy ); // NOTE: Using diffuse due to texture2DLodSpecular() has extra: d *= min(1, d * (proj_lod - lod));
        }
    }
    return slit; // specular light
}

vec3 getProjectedLightSpecularColor(float light_distance, vec2 projected_uv)
{
    float diff = clamp((light_distance - proj_focus)/proj_range, 0.0, 1.0);
    float lod = diff * proj_lod;
    vec4 plcol = getTexture2DLodDiffuse(projected_uv.xy, lod); // NOTE: Using diffuse due to texture2DLodSpecular() has extra: d *= min(1, d * (proj_lod - lod));

    return color.rgb * plcol.rgb * plcol.a;
}

vec4 getPosition(vec2 pos_screen)
{
    float depth = getDepth(pos_screen);
    vec2 sc = getScreenCoordinate(pos_screen);
    vec4 ndc = vec4(sc.x, sc.y, 2.0*depth-1.0, 1.0);
    vec4 pos = inv_proj * ndc;
    pos /= pos.w;
    pos.w = 1.0;
    return pos;
}

// get position given a normalized device coordinate
vec3 getPositionWithNDC(vec3 ndc)
{
    vec4 pos = inv_proj * vec4(ndc, 1.0);
    return pos.xyz / pos.w;
}

vec4 getPositionWithDepth(vec2 pos_screen, float depth)
{
    vec2 sc = getScreenCoordinate(pos_screen);
    vec3 ndc = vec3(sc.x, sc.y, 2.0*depth-1.0);
    return vec4(getPositionWithNDC(ndc), 1.0);
}

vec2 getScreenCoord(vec4 clip)
{
    vec4 ndc = clip;
         ndc.xyz /= clip.w;
    vec2 screen = vec2( ndc.xy * 0.5 );
         screen += 0.5;
    return screen;
}

vec2 getScreenXY(vec4 clip)
{
    vec4 ndc = clip;
         ndc.xyz /= clip.w;
    vec2 screen = vec2( ndc.xy * 0.5 );
         screen += 0.5;
         screen *= screen_res;
    return screen;
}

// Color utils

vec3 colorize_dot(float x)
{
    if (x > 0.0) return vec3( 0, x, 0 );
    if (x < 0.0) return vec3(-x, 0, 0 );
                 return vec3( 0, 0, 1 );
}

vec3 hue_to_rgb(float hue)
{
    if (hue > 1.0) return vec3(0.5);
    vec3 rgb = abs(hue * 6. - vec3(3, 2, 4)) * vec3(1, -1, -1) + vec3(-1, 2, 2);
    return clamp(rgb, 0.0, 1.0);
}

// PBR Utils

vec2 BRDF(float NoV, float roughness)
{
    return texture(brdfLut, vec2(NoV, roughness)).rg;
}

// Lagarde and de Rousiers 2014, "Moving Frostbite to PBR"
float computeSpecularAO(float NoV, float ao, float roughness)
{
    return clamp(pow(NoV + ao, exp2(-16.0 * roughness - 1.0)) - 1.0 + ao, 0.0, 1.0);
}

// set colorDiffuse and colorSpec to the results of GLTF PBR style IBL
void pbrIbl(vec3 diffuseColor,
            vec3 specularColor,
            vec3 radiance, // radiance map sample
            vec3 irradiance, // irradiance map sample
            float ao,       // ambient occlusion factor
            float nv,       // normal dot view vector
            float perceptualRough,
            out vec3 diffuseOut,
            out vec3 specularOut)
{
    // retrieve a scale and bias to F0. See [1], Figure 3
    vec2 brdf = BRDF(clamp(nv, 0, 1), 1.0-perceptualRough);
    vec3 diffuseLight = irradiance;
    vec3 specularLight = radiance;

    vec3 diffuse = diffuseLight * diffuseColor;
    vec3 specular = specularLight * (specularColor * brdf.x + brdf.y);

    diffuseOut = diffuse * ao;
    specularOut = specular * computeSpecularAO(nv, ao, perceptualRough * perceptualRough);
}


// Encapsulate the various inputs used by the various functions in the shading equation
// We store values in this struct to simplify the integration of alternative implementations
// of the shading terms, outlined in the Readme.MD Appendix.
struct PBRInfo
{
    float NdotL;                  // cos angle between normal and light direction
    float NdotV;                  // cos angle between normal and view direction
    float NdotH;                  // cos angle between normal and half vector
    float LdotH;                  // cos angle between light direction and half vector
    float VdotH;                  // cos angle between view direction and half vector
    float perceptualRoughness;    // roughness value, as authored by the model creator (input to shader)
    float metalness;              // metallic value at the surface
    vec3 reflectance0;            // full reflectance color (normal incidence angle)
    vec3 reflectance90;           // reflectance color at grazing angle
    float alphaRoughness;         // roughness mapped to a more linear change in the roughness (proposed by [2])
    vec3 diffuseColor;            // color contribution from diffuse lighting
    vec3 specularColor;           // color contribution from specular lighting
};

// Basic Lambertian diffuse
// Implementation from Lambert's Photometria https://archive.org/details/lambertsphotome00lambgoog
// See also [1], Equation 1
vec3 diffuse(PBRInfo pbrInputs)
{
    return pbrInputs.diffuseColor / M_PI;
}

// The following equation models the Fresnel reflectance term of the spec equation (aka F())
// Implementation of fresnel from [4], Equation 15
vec3 specularReflection(PBRInfo pbrInputs)
{
    return pbrInputs.reflectance0 + (pbrInputs.reflectance90 - pbrInputs.reflectance0) * pow(clamp(1.0 - pbrInputs.VdotH, 0.0, 1.0), 5.0);
}

// This calculates the specular geometric attenuation (aka G()),
// where rougher material will reflect less light back to the viewer.
// This implementation is based on [1] Equation 4, and we adopt their modifications to
// alphaRoughness as input as originally proposed in [2].
float geometricOcclusion(PBRInfo pbrInputs)
{
    float NdotL = pbrInputs.NdotL;
    float NdotV = pbrInputs.NdotV;
    float r = pbrInputs.alphaRoughness;

    float attenuationL = 2.0 * NdotL / (NdotL + sqrt(r * r + (1.0 - r * r) * (NdotL * NdotL)));
    float attenuationV = 2.0 * NdotV / (NdotV + sqrt(r * r + (1.0 - r * r) * (NdotV * NdotV)));
    return attenuationL * attenuationV;
}

// The following equation(s) model the distribution of microfacet normals across the area being drawn (aka D())
// Implementation from "Average Irregularity Representation of a Roughened Surface for Ray Reflection" by T. S. Trowbridge, and K. P. Reitz
// Follows the distribution function recommended in the SIGGRAPH 2013 course notes from EPIC Games [1], Equation 3.
float microfacetDistribution(PBRInfo pbrInputs)
{
    float roughnessSq = pbrInputs.alphaRoughness * pbrInputs.alphaRoughness;
    float f = (pbrInputs.NdotH * roughnessSq - pbrInputs.NdotH) * pbrInputs.NdotH + 1.0;
    return roughnessSq / (M_PI * f * f);
}

void pbrPunctual(vec3 diffuseColor, vec3 specularColor,
                    float perceptualRoughness,
                    float metallic,
                    vec3 n, // normal
                    vec3 v, // surface point to camera
                    vec3 l,
                    out float nl,
                    out vec3 diff,
                    out vec3 spec) //surface point to light
{
    // make sure specular highlights from punctual lights don't fall off of polished surfaces
    perceptualRoughness = max(perceptualRoughness, MIN_PBR_ROUGHNESS);

    float alphaRoughness = perceptualRoughness * perceptualRoughness;

    // Compute reflectance.
    float reflectance = max(max(specularColor.r, specularColor.g), specularColor.b);

    // For typical incident reflectance range (between 4% to 100%) set the grazing reflectance to 100% for typical fresnel effect.
    // For very low reflectance range on highly diffuse objects (below 4%), incrementally reduce grazing reflecance to 0%.
    float reflectance90 = clamp(reflectance * 25.0, 0.0, 1.0);
    vec3 specularEnvironmentR0 = specularColor.rgb;
    vec3 specularEnvironmentR90 = vec3(1.0, 1.0, 1.0) * reflectance90;

    vec3 h = normalize(l+v);                        // Half vector between both l and v
    vec3 reflection = -normalize(reflect(v, n));
    reflection.y *= -1.0f;

    float NdotL = clamp(dot(n, l), 0.001, 1.0);
    float NdotV = clamp(abs(dot(n, v)), 0.001, 1.0);
    float NdotH = clamp(dot(n, h), 0.0, 1.0);
    float LdotH = clamp(dot(l, h), 0.0, 1.0);
    float VdotH = clamp(dot(v, h), 0.0, 1.0);

    PBRInfo pbrInputs = PBRInfo(
        NdotL,
        NdotV,
        NdotH,
        LdotH,
        VdotH,
        perceptualRoughness,
        metallic,
        specularEnvironmentR0,
        specularEnvironmentR90,
        alphaRoughness,
        diffuseColor,
        specularColor
    );

    // Calculate the shading terms for the microfacet specular shading model
    vec3 F = specularReflection(pbrInputs);
    float G = geometricOcclusion(pbrInputs);
    float D = microfacetDistribution(pbrInputs);

    // Calculation of analytical lighting contribution
    vec3 diffuseContrib = (1.0 - F) * diffuse(pbrInputs);
    vec3 specContrib = F * G * D / (4.0 * NdotL * NdotV);

    nl = NdotL;

    diff = diffuseContrib;
    spec = specContrib;
}

vec3 pbrCalcPointLightOrSpotLight(vec3 diffuseColor, vec3 specularColor,
                    float perceptualRoughness,
                    float metallic,
                    vec3 n, // normal
                    vec3 p, // pixel position
                    vec3 v, // view vector (negative normalized pixel position)
                    vec3 lp, // light position
                    vec3 ld, // light direction (for spotlights)
                    vec3 lightColor,
                    float lightSize, float falloff, float is_pointlight, float ambiance)
{
    vec3 color = vec3(0,0,0);

    vec3 lv = lp.xyz - p;

    float lightDist = length(lv);

    float dist = lightDist / lightSize;
    if (dist <= 1.0)
    {
        lv /= lightDist;

        float dist_atten = calcLegacyDistanceAttenuation(dist, falloff);

        // spotlight coefficient.
        float spot = max(dot(-ld, lv), is_pointlight);
        // spot*spot => GL_SPOT_EXPONENT=2
        float spot_atten = spot*spot;

        vec3 intensity = spot_atten * dist_atten * lightColor * 3.0; //magic number to balance with legacy materials

        float nl = 0;
        vec3 diffPunc = vec3(0);
        vec3 specPunc = vec3(0);

        pbrPunctual(diffuseColor, specularColor, perceptualRoughness, metallic, n.xyz, v, lv, nl, diffPunc, specPunc);
        color = intensity * clamp(nl * (diffPunc + specPunc), vec3(0), vec3(10));
    }
    float final_scale = 1.0;
    if (classic_mode > 0)
        final_scale = 0.9;
    return color * final_scale;
}

void calcDiffuseSpecular(vec3 baseColor, float metallic, inout vec3 diffuseColor, inout vec3 specularColor)
{
    vec3 f0 = vec3(0.04);
    diffuseColor = baseColor*(vec3(1.0)-f0);
    diffuseColor *= 1.0 - metallic;
    specularColor = mix(f0, baseColor, metallic);
}

vec3 pbrBaseLight(vec3 diffuseColor, vec3 specularColor, float metallic, vec3 v, vec3 norm, float perceptualRoughness, vec3 light_dir, vec3 sunlit, float scol, vec3 radiance, vec3 irradiance, vec3 colorEmissive, float ao, vec3 additive, vec3 atten)
{
    perceptualRoughness = max(perceptualRoughness, MIN_PBR_ROUGHNESS);
    vec3 color = vec3(0);

    float NdotV = clamp(abs(dot(norm, v)), 0.001, 1.0);
    vec3 iblDiff = vec3(0);
    vec3 iblSpec = vec3(0);
    pbrIbl(diffuseColor, specularColor, radiance, irradiance, ao, NdotV, perceptualRoughness, iblDiff, iblSpec);

    color += iblDiff;

    // For classic mode, we use a special version of pbrPunctual that basically gives us a deconstructed form of the lighting.
    float nl = 0;
    vec3 diffPunc = vec3(0);
    vec3 specPunc = vec3(0);
    pbrPunctual(diffuseColor, specularColor, perceptualRoughness, metallic, norm, v, normalize(light_dir), nl, diffPunc, specPunc);

    // Depending on the sky, we combine these differently.
    if (classic_mode > 0)
    {
        irradiance.rgb = srgb_to_linear(irradiance * 0.9); // BINGO

        // Reconstruct the diffuse lighting that we do for blinn-phong materials here.
        // A special note about why we do some really janky stuff for classic mode.
        // Since adding classic mode, we've moved the lambertian diffuse multiply out from pbrPunctual and instead handle it in the different light type calcs.
        // This will never be 100% correct, but at the very least we can make it look mostly correct with legacy skies and classic mode.

        float da = pow(nl, 1.2);

        vec3 sun_contrib = vec3(min(da, scol));

        // Multiply by PI to account for lambertian diffuse colors.  Otherwise things will be too dark when lit by the sun on legacy skies.
        sun_contrib = srgb_to_linear(linear_to_srgb(sun_contrib) * sunlit * 0.7) * M_PI;

        // Manually recombine everything here.  We have to separate the shading to ensure that lighting is able to more closely match blinn-phong.
        vec3 finalAmbient = irradiance.rgb * diffuseColor.rgb; // BINGO
        vec3 finalSun = clamp(sun_contrib * ((diffPunc.rgb + specPunc.rgb) * scol), vec3(0), vec3(10)); // QUESTIONABLE BINGO?
        color.rgb = srgb_to_linear(linear_to_srgb(finalAmbient) + (linear_to_srgb(finalSun) * 1.1));
        //color.rgb = sun_contrib * diffuseColor.rgb;
    }
    else
    {
        color += clamp(nl * (diffPunc + specPunc), vec3(0), vec3(10)) * sunlit * 3.0 * scol;
    }

    color.rgb += iblSpec.rgb;

    color += colorEmissive;

    return color;
}

uniform vec4 waterPlane;
uniform float waterSign;

// discard if given position in eye space is on the wrong side of the waterPlane according to waterSign
void waterClip(vec3 pos)
{
    // TODO: make this less branchy
    if (waterSign > 0)
    {
        if ((dot(pos.xyz, waterPlane.xyz) + waterPlane.w) < 0.0)
        {
            discard;
        }
    }
    else
    {
        if ((dot(pos.xyz, waterPlane.xyz) + waterPlane.w) > 0.0)
        {
            discard;
        }
    }

}
